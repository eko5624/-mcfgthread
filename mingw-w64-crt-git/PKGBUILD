# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=crt
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=10.0.0.r269.g70ec1e0de
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://mingw-w64.sourceforge.io/'
license=('custom')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-headers-git")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-binutils"
             "${MINGW_PACKAGE_PREFIX}-autotools")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
options=('!strip' '!buildflags' '!emptydirs')
source=("mingw-w64"::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64"
        0001-Allow-to-use-bessel-and-complex-functions-without-un.patch
        9001-crt-Mark-atexit-as-DATA-because-it-s-always-overridd.patch
        9002-crt-Provide-wrappers-for-exit-in-libmingwex.patch
        9003-crt-Implement-standard-conforming-termination-suppor.patch
        9004-crt-Copy-clock-and-nanosleep-from-winpthreads.patch)
sha256sums=('SKIP'
            'd641257f7e1469aff89adc33e57702b75fe9667ca035978f890eae1020b6814c'
            'd4370b1e19f8e272f16e5af40faf74821e81d0b20783ee7ab2b32fe0a21466ee'
            '5d18a39a421ef5b3ea28146041a3ee4d05b6781dde020565a15cf1383a810055'
            '08b3602239fb873988af1783d3bdde7579b725198e92ef5f03569de9ac925910'
            'e56ef63443fb147489a0ea7d866daf49482773bc1aa779e98496f1ebe0ce6f70')

pkgver() {
  cd "${srcdir}/mingw-w64"
  git describe --long HEAD | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd ${srcdir}/mingw-w64
  git reset --hard
  git clean -fdx

  git apply "${srcdir}/9001-crt-Mark-atexit-as-DATA-because-it-s-always-overridd.patch"
  git apply "${srcdir}/9002-crt-Provide-wrappers-for-exit-in-libmingwex.patch"
  git apply "${srcdir}/9003-crt-Implement-standard-conforming-termination-suppor.patch"
  git apply "${srcdir}/9004-crt-Copy-clock-and-nanosleep-from-winpthreads.patch"
  (cd mingw-w64-crt && automake)

  git apply "${srcdir}/0001-Allow-to-use-bessel-and-complex-functions-without-un.patch"
}

build() {
  msg "Building ${MSYSTEM} CRT"

  declare -a _crt_configure_args
  case "$CARCH" in
    i686)
      _crt_configure_args=("--disable-lib64" "--enable-lib32")
    ;;
    x86_64)
      _crt_configure_args=("--disable-lib32" "--enable-lib64")
    ;;
    armv7)
      _crt_configure_args=("--disable-lib32" "--disable-lib64" "--enable-libarm32")
    ;;
    aarch64)
      _crt_configure_args=("--disable-lib32" "--disable-lib64" "--disable-libarm32" "--enable-libarm64")
    ;;
  esac

  # only clang+lld support this at the moment
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]]; then
    _crt_configure_args+=("--enable-cfguard")
  fi

  local _default_msvcrt=msvcrt
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]] || [[ $MINGW_PACKAGE_PREFIX == *-ucrt-* ]]; then
    _default_msvcrt=ucrt
  fi

  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  ${srcdir}/mingw-w64/mingw-w64-crt/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-default-msvcrt=${_default_msvcrt} \
    --enable-wildcard \
    --disable-dependency-tracking \
    "${_crt_configure_args[@]}"

  make
}

package() {
  msg "Installing ${MSYSTEM} crt"
  cd ${srcdir}/build-${MSYSTEM}
  make DESTDIR=${pkgdir} install-strip

  # Create empty dummy archives, to avoid failing when the compiler driver
  # adds -lssp -lssh_nonshared when linking.
  ar rcs "${pkgdir}"${MINGW_PREFIX}/lib/libssp.a
  ar rcs "${pkgdir}"${MINGW_PREFIX}/lib/libssp_nonshared.a

  msg "Installing MinGW-w64 licenses"
  install -Dm644 ${srcdir}/mingw-w64/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 ${srcdir}/mingw-w64/COPYING.MinGW-w64/COPYING.MinGW-w64.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MinGW-w64.txt
  install -Dm644 ${srcdir}/mingw-w64/COPYING.MinGW-w64-runtime/COPYING.MinGW-w64-runtime.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MinGW-w64-runtime.txt
}
