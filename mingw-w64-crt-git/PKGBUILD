# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=crt
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=11.0.0.r769.gcff4b8fda
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://www.mingw-w64.org/'
msys2_repository_url="https://sourceforge.net/p/mingw-w64/mingw-w64/ci/master/tree/mingw-w64-crt/"
license=('spdx:ZPL-2.1')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-headers-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-binutils"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "git")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
options=('!strip' '!buildflags' '!emptydirs')
source=("mingw-w64"::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64"
        0001-Allow-to-use-bessel-and-complex-functions-without-un.patch
        9001-crt-Mark-atexit-as-DATA-because-it-s-always-overridd.patch
        9002-crt-Provide-wrappers-for-exit-in-libmingwex.patch
        9003-crt-Implement-standard-conforming-termination-suppor.patch
        9004-crt-Copy-clock-and-nanosleep-from-winpthreads.patch)
sha256sums=('SKIP'
            'd641257f7e1469aff89adc33e57702b75fe9667ca035978f890eae1020b6814c'
            'a8c5ce90154c9e02877660f044cfd4e312788d3a5a88a4f27131e511c58a25c1'
            '86f6dd6b451ea30b742d5a1cc7c2f3b8b43e131d0a16663264d22ee40996e328'
            '169dd1b05a7622ab6d87bd69b8446584975002553668a529d437cecb0735802d'
            '8d21a3916f39455f1b422031bfb21308f8e90fdb272f6e09d54f1a522f53a491')

pkgver() {
  cd "${srcdir}/mingw-w64"
  git describe --long HEAD | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd ${srcdir}/mingw-w64
  git reset --hard
  git clean -fdx

  git apply "${srcdir}/9001-crt-Mark-atexit-as-DATA-because-it-s-always-overridd.patch"
  git apply "${srcdir}/9002-crt-Provide-wrappers-for-exit-in-libmingwex.patch"
  git apply "${srcdir}/9003-crt-Implement-standard-conforming-termination-suppor.patch"
  git apply "${srcdir}/9004-crt-Copy-clock-and-nanosleep-from-winpthreads.patch"
  (cd mingw-w64-crt && autoreconf)

  git apply "${srcdir}/0001-Allow-to-use-bessel-and-complex-functions-without-un.patch"
}

build() {
  declare -a _extra_config
  case "$CARCH" in
    i686)
      _extra_config=("--disable-lib64" "--enable-lib32")
    ;;
    x86_64)
      _extra_config=("--disable-lib32" "--enable-lib64")
    ;;
    armv7)
      _extra_config=("--disable-lib32" "--disable-lib64" "--enable-libarm32")
    ;;
    aarch64)
      _extra_config=("--disable-lib32" "--disable-lib64" "--disable-libarm32" "--enable-libarm64")
    ;;
  esac

  # only clang+lld support this at the moment
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]]; then
    _extra_config+=("--enable-cfguard")
  fi

  if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]] || [[ $MINGW_PACKAGE_PREFIX == *-ucrt-* ]]; then
    _extra_config+=("--with-default-msvcrt=ucrt")
  else
    _extra_config+=("--with-default-msvcrt=msvcrt")
  fi

  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  ${srcdir}/mingw-w64/mingw-w64-crt/configure \
    --prefix=${MINGW_PREFIX} \
    --enable-wildcard \
    --disable-dependency-tracking \
    "${_extra_config[@]}"

  make
}

package() {
  cd ${srcdir}/build-${MSYSTEM}
  make DESTDIR=${pkgdir} install-strip

  # Create empty dummy archives, to avoid failing when the compiler driver
  # adds -lssp -lssh_nonshared when linking.
  ar rcs "${pkgdir}"${MINGW_PREFIX}/lib/libssp.a
  ar rcs "${pkgdir}"${MINGW_PREFIX}/lib/libssp_nonshared.a

  install -Dm644 ${srcdir}/mingw-w64/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 ${srcdir}/mingw-w64/COPYING.MinGW-w64/COPYING.MinGW-w64.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MinGW-w64.txt
  install -Dm644 ${srcdir}/mingw-w64/COPYING.MinGW-w64-runtime/COPYING.MinGW-w64-runtime.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MinGW-w64-runtime.txt
}
